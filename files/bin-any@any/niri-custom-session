#!/usr/bin/env nu

def main [] { }

def 'main start' [] {
   let shell_env = env | lines | split column '=' key value
   systemctl --user --wait start niri-custom.service

   let systemd_env = systemctl --user show-environment
   | lines
   | split column '=' key value

   let shell_and_systemd_env = $shell_env | join -o $systemd_env key

   let unset_or_update = $shell_and_systemd_env | par-each {|row|
      if $row.value == null {
         return {unset: $row.key}
      }

      if $row.value_ == null {
         return
      }

      if $row.value_ != $row.value {
         return {update: $row.key}
      }
   }

   let systemd_env_to_unset = $unset_or_update | get -o unset | compact
   let systemd_env_to_update = $unset_or_update | get -o update | compact

   if ($systemd_env_to_unset | is-not-empty) {
      systemctl --user unset-environment ...$systemd_env_to_unset
   }

   if ($systemd_env_to_update | is-not-empty) {
      systemctl --user import-environment ...$systemd_env_to_update
   }
}

def 'main stop' [] {
   (
      systemctl
      --user
      stop
      app-graphical.slice
      background-graphical.slice
      session-graphical.slice
      graphical-session.target
   )
}
